
model {
p0 ~ dunif(0, 1) # baseline encounter probability
sigma ~ dunif(0, 2) # scale parameter of encounter function
psi ~ dunif(0, 1) # DA parameter: E(N) = M*psi

for(i in 1:M) {
z[i] ~ dbern(psi) # Is individual real?
s[i,1] ~ dunif(xlim[1], xlim[2]) # x-coordinate of activity center
s[i,2] ~ dunif(ylim[1], ylim[2]) # y-coordinate

for(j in 1:J) {
# dist between activity center and trap
d[i,j] <- sqrt((s[i,1] - x[j,1])^2 + (s[i,2] - x[j,2])^2)
p[i,j] <- p0*exp(-d[i,j]^2/(2*sigma^2)) # capture prob at trap j

for(k in 1:K) {
y[i,j,k] ~ dbern(p[i,j]*z[i]) # model for data
}
}
}
N <- sum(z) # realized abundance
EN <- M*psi # expected abundance
A <- (xlim[2]-xlim[1])*(ylim[2]-ylim[1]) # area of state-space
D <- N/A # realized density
ED <- EN/A # expected density
}
